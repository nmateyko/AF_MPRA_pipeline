configfile: "test_config.yaml"

def get_barcode_fastq(wildcards):
    sample_file = config["samples"][wildcards.sample]['barcode']
    data_dir = config['data_dir']
    return f"{data_dir}{sample_file}"

def get_second_fastq(wildcards):
    sample_file = config["samples"][wildcards.sample]['second']
    data_dir = config['data_dir']
    return f"{data_dir}{sample_file}"

def get_fastq_bowtie(wildcards):
    data_dir = config['data_dir']
    if wildcards.sample == "A7":
        R2_file = config["samples"][wildcards.sample]['second']
        return [f"output/trimmed/{wildcards.sample}.fastq.gz", f"{data_dir}{R2_file}"]
    else:
        R1_file = config["samples"][wildcards.sample]['second']
        return [f"{data_dir}{R1_file}", f"output/trimmed/{wildcards.sample}.fastq.gz"]

rule all:
    localrule: True
    input:
        expand("output/merged/{sample}.csv", sample=config["samples"])


rule get_barcode:
    input:
        get_barcode_fastq
    output: 
        csv="output/barcodes/{sample}.csv",
        fastq="output/trimmed/{sample}.fastq.gz"
    params:
        upstream_len = lambda wc: config["upstream_len"][wc.sample],
        bc_len = lambda wc: config["bc_len"][wc.sample],
        downstream_len = lambda wc: config["downstream_len"][wc.sample],
    conda:
        "environment.yaml"
    threads: 1
    resources:
        mem_mb=4800,
        time="02:00:00"
    script: "scripts/make_BC_fastq_R1.py"

rule align:
    input:
        get_fastq_bowtie
    output: 
        "output/aligned/{sample}_unaligned_pairs.1.fastq.gz",
        "output/aligned/{sample}_unaligned_pairs.2.fastq.gz",
        "output/aligned/{sample}_aligned_pairs.1.fastq.gz",
        "output/aligned/{sample}_aligned_pairs.2.fastq.gz",
        sam = "output/aligned/{sample}.sam",
    log:
        "output/logs/bowtie/{sample}.log"
    params:
        bowtie_index = lambda wc: config["index"][wc.sample],
        unal_prefix = "output/aligned/{sample}_unaligned_pairs.%.fastq.gz",
        al_prefix = "output/aligned/{sample}_aligned_pairs.%.fastq.gz",
    conda:
        "environment.yaml"
    threads: 4
    resources:
        mem_mb=16000,
        time="04:00:00"
    shell:
        "bowtie2 -x {params.bowtie_index} "
        "--un-conc-gz {params.unal_prefix} --al-conc-gz {params.al_prefix} "
        "-1 {input[0]} -2 {input[1]} -S {output.sam} --fr -p 4 --reorder 2> {log}"

rule merge_sam_BC:
    input:
        barcodes="output/barcodes/{sample}.csv",
        alignment="output/aligned/{sample}.sam"
    output: 
        "output/merged/{sample}.csv"
    conda:
        "environment.yaml"
    threads: 1
    resources:
        mem_mb=4800,
        time="04:00:00"
    script: "scripts/merge_sam_BC.py"